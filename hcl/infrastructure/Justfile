DEFAULT_STORAGE_LABEL := "frankenstorage"
DEFAULT_REGISTRY_NAME := "frankistry"

# Print available recipes by default.
_default:
    just --list

##############
# ESSENTIALS #
##############

# Setup for development.
setup:
    brew bundle
    pre-commit install
    tofu init

# Upgrade things.
update:
    brew bundle
    pre-commit autoupdate

# Run linters.
lint *PRE_COMMIT_RUN_PARAMS:
    pre-commit run --all-files {{PRE_COMMIT_RUN_PARAMS}}

##################
# INFRASTRUCTURE #
##################

# Deploy infrastructure.
infrastructure:
    tofu apply -auto-approve

plan:
    tofu plan

storage-id LABEL=DEFAULT_STORAGE_LABEL:
    vultr object-storage list | awk -v c=1 '/{{LABEL}}/ {print $1; c=0} END {exit c}'

# Print S3 keys.
storage-keys:
    vultr object-storage get "$(just storage-id)"

registry-id NAME=DEFAULT_REGISTRY_NAME:
    vultr container-registry list | awk -v c=1 '/{{NAME}}/ {print $1; c=0} END {exit c}'
